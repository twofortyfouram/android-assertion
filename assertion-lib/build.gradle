apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/twofortyfouram/maven' }
}

dependencies {
    compile group:'net.jcip', name:'jcip-annotations', version:'1.0'
    compile group:'com.twofortyfouram', name:'android-annotation', version:'1.+'
    instrumentTestCompile group:'com.twofortyfouram', name:'android-test', version:'1.+'
}

android {
    compileSdkVersion Integer.parseInt(ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(ANDROID_VERSION_CODE)
        versionName ANDROID_VERSION_NAME
        consumerProguardFiles 'proguard-consumer.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    release {
        // Disable ProGuard to resolve bad RuntimeInvisibleParameterAnnotations error
        // runProguard true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.plugin.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(id: MAVEN_ID, url: MAVEN_URL) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
            pom.groupId = MAVEN_GROUP
            pom.version = ANDROID_VERSION_NAME
            pom.artifactId = MAVEN_ARTIFACT_NAME
        }
    }
}
